plugins {
    id 'com.android.library'
    id 'maven-publish'
}

group = 'com.github.cooklang'
version = project.versionName

def gprProperties = new Properties()
file("../gpr.properties").withInputStream { gprProperties.load(it) }

android {
    namespace 'com.github.instacart.ahoy'
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode project.versionCode as Integer
        versionName project.versionName

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        buildConfigField 'String', 'VERSION_NAME', "\"${project.versionName}\""

        aarMetadata {
            minCompileSdk = 21
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/**'
    }

    buildFeatures {
        buildConfig true
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    lint {
        baseline = file("lint-baseline.xml")
    }

    lintOptions {
        disable 'TimberTagLength'
        disable 'LogNotTimber'
        abortOnError false
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.7.1'
    implementation 'androidx.core:core:1.13.1'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    annotationProcessor "com.google.auto.value:auto-value:${project.autoValueVersion}"
    compileOnly "com.google.auto.value:auto-value-annotations:${project.autoValueVersion}"

    implementation "com.ryanharter.auto.value:auto-value-parcel-adapter:${project.autoValueParcelVersion}"
    annotationProcessor "com.ryanharter.auto.value:auto-value-parcel:${project.autoValueParcelVersion}"

    implementation "com.fasterxml.jackson.core:jackson-annotations:${project.jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${project.jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${project.jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${project.jacksonVersion}"

    implementation "com.squareup.okhttp3:logging-interceptor:${project.okhttpVersion}"
    implementation "com.squareup.okhttp3:okhttp:${project.okhttpVersion}"

    implementation "com.squareup.retrofit2:adapter-rxjava3:${project.retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-jackson:${project.retrofitVersion}"
    implementation "com.squareup.retrofit2:retrofit:${project.retrofitVersion}"

    implementation 'io.reactivex.rxjava3:rxjava:3.1.8'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:2.25.1'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.15.2'
}

publishing {
    publications {
        ahoyAndroid(MavenPublication) {
            groupId = 'com.github.cooklang'
            artifactId = 'ahoy-android'
            version = project.versionName
            afterEvaluate {
                from components.release
            }

            pom {
                name = 'ahoy-android'
                description = 'Cooklang Parser Library for Android'
                url = 'https://github.com/cooklang/ahoy-android'

                scm {
                    connection = 'scm:git:git://github.com/cooklang/ahoy-android.git'
                    developerConnection = 'scm:git:ssh://github.com:cooklang/ahoy-android.git'
                    url = 'https://github.com/cooklang/ahoy-android'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cooklang/ahoy-android")
            credentials {
                username = gprProperties['gpr.user']
                password = gprProperties['gpr.token']
            }
        }
    }
}

tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

artifacts {
    archives sourcesJar
}
